(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)

(FILECREATED "28-May-2023 08:38:28" {DSK}<home>medley>il>BRAINCONS.;54 25685  

      :EDIT-BY "PA"

      :CHANGES-TO (FNS BRC.VM.IN)

      :PREVIOUS-DATE "27-May-2023 09:59:25" {DSK}<home>medley>il>BRAINCONS.;53)


(PRETTYCOMPRINT BRAINCONSCOMS)

(RPAQQ BRAINCONSCOMS
       ((RECORDS BRANCH BRC.ERROR BRC.PROGRAM BRC.VM)
        (FNS BRC.BRANCHP BRC.MAKE.JNZ BRC.MAKE.JZ BRC.MAKE.VM BRC.PARSE BRC.PARSE.INSTRUCTION 
             BRC.VALIDATE.BRANCHES BRC.VM.BRANCH BRC.VM.IN BRC.VM.INCDEC BRC.VM.LOAD BRC.VM.OUT 
             BRC.VM.PREVNEXT BRC.VM.RESET BRC.VM.RUN)
        (VARS (BRC.MAXIP 30000)
              (* Maximum number of instructions in a program.))
        (INITVARS (BRC.MEMSIZE 30000)
               (* Number of memory cells.))))
(DECLARE%: EVAL@COMPILE

(RECORD BRANCH 

         (* A branch instruction consisting of a MNEMONIC, the index ADDRESS in the 
         instructions array, the DESTINATION address, and the SRCPTR index of the branch 
         command in the source string.)

               (MNEMONIC ADDRESS DESTINATION SRCPTR))

(RECORD BRC.ERROR 

         (* An error caused by REASON, a symbol, occurring at the SRCIDX character index 
         in the source and at ADDRESS in the program, if applicable.)

                  (REASON SRCIDX ADDRESS))

(RECORD BRC.PROGRAM 

         (* Intermediate program representation holding an array of INSTRUCTIONS, the 
         index LASTIP of the last instruction in the array, a list of branch instructions, 
         a list of BRC.ERROR parse errors, and the SOURCE code as a string.)

                    (INSTRUCTIONS LASTIP BRANCHES ERRORS SOURCE))

(RECORD BRC.VM 

         (* A record representing a Brainfuck Virtual Machine that holds the main memory, 
         I/O streams, and a program.)

               (PROGRAM                                      (* 
                                                             #.(SEDIT::MAKE-BROKEN-ATOM "PROGRAM:") 
                                                             program, a BRC.PROGRAM)
                      IP                                     (* #.(SEDIT::MAKE-BROKEN-ATOM "IP:") 
                                                             program counter, the address of the 
                                                             next instruction)
                      MEMORY                                 (* 
                                                             #.(SEDIT::MAKE-BROKEN-ATOM "MEMORY:") 
                                                             0-based array of BRC.MEMSIZE memory 
                                                             cells holding 8-bit values)
                      DATAPTR                                (* 
                                                             #.(SEDIT::MAKE-BROKEN-ATOM "DATAPTR:") 
                                                             index of the memory cell the next 
                                                             instruction acts on)
                      STATE

         (* #.(SEDIT::MAKE-BROKEN-ATOM "STATE:") the state of the VM, one of READY %(ready 
         to load or run a program, or after program execution terminated with no errors%) 
         or a BRC.ERROR describing a runtime error)

                      IN                                     (* #.(SEDIT::MAKE-BROKEN-ATOM "IN:") 
                                                             stream to get program input, NIL by 
                                                             default)
                      OUT                                    (* #.(SEDIT::MAKE-BROKEN-ATOM "OUT:") 
                                                             stream to display program output, NIL 
                                                             by default)
                      ))
)
(DEFINEQ

(BRC.BRANCHP
  [LAMBDA (INSTRUCTION)                                      (* ; "Edited 15-Mar-2023 11:25 by PA")
                                                             (* ; "Edited 26-Feb-2023 09:03 by PA")
                                                             (* Returns T if INSTRUCTION is a 
                                                             branch instruction.)
    (AND (LISTP INSTRUCTION)
         (EQ (LENGTH INSTRUCTION)
             4)
         (LET ((MNEMONIC (fetch (BRANCH MNEMONIC) of INSTRUCTION)))
              (AND (LITATOM MNEMONIC)
                   (OR (EQ MNEMONIC 'JZ)
                       (EQ MNEMONIC 'JNZ])

(BRC.MAKE.JNZ
  [LAMBDA (SRC.IDX ADDR PROGRAM)                             (* ; "Edited 15-Mar-2023 11:27 by PA")
                                                             (* ; "Edited  4-Mar-2023 10:44 by PA")
                                                             (* ; "Edited 26-Feb-2023 09:14 by PA")

         (* Creates and returns the JNZ instruction at position SRC.IDX in the source 
         string, taking care of backpatching the destination address of the matching JZ 
         instruction and updating the branches and errors of PROGRAM.
         Uses the free variable BRC.BRANCH.STACK.)

    (LET ((JZ (pop BRC.BRANCH.STACK))
          (INSTRUCTION (create BRANCH
                              MNEMONIC _ 'JNZ
                              ADDRESS _ ADDR
                              SRCPTR _ SRC.IDX)))
         (if (AND (BRC.BRANCHP JZ)
                  (EQ (fetch (BRANCH MNEMONIC) of JZ)
                      'JZ))
             then (replace (BRANCH DESTINATION) of JZ with (ADD1 ADDR))
                  (replace (BRANCH DESTINATION) of INSTRUCTION with (ADD1 (fetch (BRANCH ADDRESS)
                                                                             of JZ)))
           else (push (fetch (BRC.PROGRAM ERRORS) of PROGRAM)
                      (create BRC.ERROR
                             REASON _ 'UNMATCHEDBRANCH
                             SRCIDX _ SRC.IDX
                             ADDRESS _ ADDR)))
         (push (fetch (BRC.PROGRAM BRANCHES) of PROGRAM)
               INSTRUCTION)
         INSTRUCTION])

(BRC.MAKE.JZ
  [LAMBDA (SRC.IDX ADDR PROGRAM)                             (* ; "Edited 15-Mar-2023 11:04 by PA")
                                                             (* ; "Edited  4-Mar-2023 10:46 by PA")
                                                             (* ; "Edited 25-Feb-2023 11:08 by PA")

         (* Creates and returns the JZ instruction at position SRC.IDX in the source 
         string #.(SEDIT::MAKE-BROKEN-ATOM ",") leaving the destination address 
         unspecified at 0 for later backpatching and updating the branches of PROGRAM.
         Uses the free variable BRC.BRANCH.STACK.)
                                                             (* ; "Edited 25-Feb-2023 11:01 by PA")
    (LET ((INST (create BRANCH
                       MNEMONIC _ 'JZ
                       ADDRESS _ ADDR
                       DESTINATION _ 0
                       SRCPTR _ SRC.IDX)))
         (push BRC.BRANCH.STACK INST)
         (push (fetch (BRC.PROGRAM BRANCHES) of PROGRAM)
               INST)
         INST])

(BRC.MAKE.VM
  [LAMBDA NIL                                                (* ; "Edited 21-May-2023 08:54 by PA")
                                                             (* ; "Edited  7-May-2023 10:09 by PA")

         (* Creates a VM. Returns the new VM with memory initialized to null bytes and no 
         program loaded.)

    (create BRC.VM
           PROGRAM _ NIL
           IP _ 1
           MEMORY _ (ARRAY BRC.MEMSIZE 'FIXP 0 0)
           DATAPTR _ 0
           STATE _ 'READY
           IN _ NIL
           OUT _ NIL])

(BRC.PARSE
  [LAMBDA (SOURCE)                                           (* ; "Edited  5-May-2023 06:59 by PA")
                                                             (* ; "Edited 11-Mar-2023 09:21 by PA")
                                                             (* ; "Edited  5-Mar-2023 10:46 by PA")
                                                             (* ; "Edited 26-Feb-2023 09:13 by PA")
                                                             (* ; "Edited 21-Feb-2023 10:49 by PA")
                                                             (* ; "Edited 18-Feb-2023 11:26 by PA")
                                                             (* ; "Edited  9-Feb-2023 10:19 by PA")
                                                             (* ; "Edited  6-Feb-2023 08:55 by PA")

         (* Parses the SOURCE string of a program and returns a BRC.PROGRAM record holding 
         the internal representation of the program and its instructions.
         Sets the ERRORS field of BRC.PROGRAM in case of parse errors, and also if SOURCE 
         is null or has more instructions than available memory.
         Binds the free variable BRC.BRANCH.STACK used by the called functions.)

    (PROG ((LASTIDX (NCHARS SOURCE))
           INST INSTS (ADDR 0)
           BRC.BRANCH.STACK)

         (* LASTIDX <= 0 Null source string, empty program.
         LASTIDX > BRC.MAXIP More instructions than available memory)

          (if (OR (ILEQ LASTIDX 0)
                  (GREATERP LASTIDX BRC.MAXIP))
              then (RETURN (create BRC.PROGRAM
                                  ERRORS _ (LIST (BRC.MAKE.ERROR 'INVALIDMEMORY LASTIDX LASTIDX))
                                  LASTIP _ 0
                                  SOURCE _ SOURCE)))
          (SETQ INSTS (ARRAY LASTIDX 'POINTER))
          (SETQ PARSED (create BRC.PROGRAM
                              INSTRUCTIONS _ INSTS
                              LASTIP _ 0
                              SOURCE _ SOURCE))

         (* ADDR hasn't been incremented yet to point to the current instruction when the 
         loop condition is tested by calling BRC.PARSE.INSTRUCTION, so add 1 to the 
         latter's argument address to account for this.)

          (for CHRIDX from 1 to LASTIDX when (SETQ INST (BRC.PARSE.INSTRUCTION (SUBSTRING SOURCE 
                                                                                      CHRIDX CHRIDX)
                                                               CHRIDX
                                                               (ADD1 ADDR)
                                                               PARSED))
             do (SETQ ADDR (ADD1 ADDR))
                (SETA INSTS ADDR INST))
          (replace (BRC.PROGRAM LASTIP) of PARSED with ADDR)
          (BRC.VALIDATE.BRANCHES PARSED)                     (* SOURCE contains no instructions, 
                                                             possibly only comments.)
          [if (ILEQ ADDR 0)
              then (replace (BRC.PROGRAM ERRORS) of PARSED with (LIST (BRC.MAKE.ERROR 'INVALIDMEMORY
                                                                             LASTIDX ADDR]
          (RETURN PARSED])

(BRC.PARSE.INSTRUCTION
  [LAMBDA (SRC.CHAR SRC.IDX ADDRESS PROGRAM)                 (* ; "Edited 27-May-2023 02:44 by PA")
                                                             (* ; "Edited 15-Mar-2023 11:04 by PA")
                                                             (* ; "Edited  4-Mar-2023 10:34 by PA")
                                                             (* ; "Edited 26-Feb-2023 09:19 by PA")
                                                             (* ; "Edited 21-Feb-2023 11:03 by PA")
                                                             (* ; "Edited  9-Feb-2023 10:32 by PA")
                                                             (* ; "Edited  5-Feb-2023 10:49 by PA")

         (* Parses the single character string SRC.CHAR at index SRC.IDX of the source of 
         PROGRAM and returns the corresponding instruction at ADDRESS, or NIL if the 
         character is not recognized.)

    (SELECTQ (MKATOM SRC.CHAR)
        (> 'NEXT)
        (< 'PREV)
        (+ 'INC)
        (- 'DEC)
        (%, 'IN)
        (%. 'OUT)
        (%[ (BRC.MAKE.JZ SRC.IDX ADDRESS PROGRAM))
        (%] (BRC.MAKE.JNZ SRC.IDX ADDRESS PROGRAM))
        NIL])

(BRC.VALIDATE.BRANCHES
  [LAMBDA (PROGRAM)                                          (* ; "Edited 25-May-2023 03:06 by PA")
                                                             (* ; "Edited 11-Mar-2023 10:02 by PA")

         (* Checks for errors the branches of PROGRAM, adds any IPOUTOFBOUNDS entries to 
         the error list of PROGRAM, and returns the list of errors.)

    (LET* [(BRANCHES (fetch (BRC.PROGRAM BRANCHES) of PROGRAM))
           (LASTIP (fetch (BRC.PROGRAM LASTIP) of PROGRAM))
           (ERRORS (for BRANCH in BRANCHES when (OR (LESSP (fetch (BRANCH DESTINATION) of BRANCH)
                                                           1)
                                                    (GREATERP (fetch (BRANCH DESTINATION)
                                                                 of BRANCH)
                                                           LASTIP))
                      collect (create BRC.ERROR
                                     REASON _ 'IPOUTOFBOUNDS
                                     ADDRESS _ (fetch (BRANCH ADDRESS) of BRANCH]
          (if ERRORS
              then (replace (BRC.PROGRAM ERRORS) of PROGRAM with (APPEND (fetch (BRC.PROGRAM ERRORS)
                                                                            of PROGRAM)
                                                                        ERRORS)))
          ERRORS])

(BRC.VM.BRANCH
  [LAMBDA (VM BRANCH)                                        (* ; "Edited 26-May-2023 04:46 by PA")

         (* Executes the JZ or JNZ instruction of the Brainfuck program in virtual machine 
         VM depending on the type of BRANCH. Generates IPOUTOFBOUNDS error if the 
         machine's IP ends up pointing to an address beyond the program.
         Returns VM.)

    (LET ((NEWIP (fetch (BRANCH DESTINATION) of BRANCH)))
         (if [AND (GEQ NEWIP 0)
                  (LEQ NEWIP (fetch (BRC.PROGRAM LASTIP) of (fetch (BRC.VM PROGRAM) of VM]
             then 

         (* The fetch-decode-execute cycle will increment IP by 1, so we account for this 
         by subtracting 1 to the destination address.)

                  (replace (BRC.VM IP) of VM with (SUB1 NEWIP))
           else (replace (BRC.VM STATE) of VM with (create BRC.ERROR
                                                          REASON _ 'IPOUTOFBOUNDS
                                                          ADDRESS _ NEWIP)))
         VM])

(BRC.VM.IN
  [LAMBDA (VM)                                               (* ; "Edited 28-May-2023 08:34 by PA")

         (* Executes the IN instruction of the Brainfuck program in virtual machine VM by 
         reading from the IN stream of VM an integer between -128 and 127 inclusive.
         Returns VM.)
                                                             (*)

         (* Valid input is either a single ASCII character, whose code is set as the value 
         of the current memory cell, or a backslash character immediately followed by an 
         optionally signed decimal integer in the designated range, whose code is again 
         set as the value of the current memory cell.
         Any characters that follow valid input are ignored.
         Blank input leaves the cell as is.)
                                                             (* ; "Edited 27-May-2023 08:59 by PA")
    (with BRC.VM VM (LET [(VALUE (bind (RAWINPUT _ NIL)
                                       (INPUTVAL _ NIL)
                                    do (SETQ RAWINPUT (PROMPTFORWORD "Input:" NIL NIL IN))
                                       [SETQ INPUTVAL (COND
                                                         [(STREQUAL (SUBSTRING RAWINPUT 1 1)
                                                                 "\")
                                                          (READ (OPENSTRINGSTREAM (SUBSTRING RAWINPUT
                                                                                         2]
                                                         ((NULL RAWINPUT)
                                                          (ELT MEMORY DATAPTR))
                                                         (T (CHCON1 RAWINPUT]
                                    repeatuntil (AND (FIXP INPUTVAL)
                                                     (GEQ INPUTVAL -128)
                                                     (LEQ INPUTVAL 127)) finally (RETURN INPUTVAL]
                         (SETA MEMORY DATAPTR VALUE)))
    VM])

(BRC.VM.INCDEC
  [LAMBDA (VM OPCODE)                                        (* ; "Edited 21-May-2023 08:57 by PA")

         (* Executes the INC or DEC instruction of the Brainfuck program in virtual 
         machine VM depending on the corresponding symbol OPCODE.
         Doesn't generate runtime errors. Returns VM.)

    (LET* ((CURVAL (ELT (fetch (BRC.VM MEMORY) of VM)
                        (fetch (BRC.VM DATAPTR) of VM)))
           (NEWVAL (SELECTQ OPCODE
                       (INC (ADD1 CURVAL))
                       (DEC (SUB1 CURVAL))
                       CURVAL)))
          (SETA (fetch (BRC.VM MEMORY) of VM)
                (fetch (BRC.VM DATAPTR) of VM)
                (COND
                   ((GREATERP NEWVAL 127)
                    -128)
                   ((LESSP NEWVAL -128)
                    127)
                   (T NEWVAL)))
          VM])

(BRC.VM.LOAD
  [LAMBDA (VM PROGRAM)                                       (* ; "Edited  7-May-2023 10:14 by PA")
                                                             (* ; "Edited  6-May-2023 10:13 by PA")

         (* Loads PROGRAM into VM and prepares the machine to start the program.
         PROGRAM is a BRC.PROGRAM. Returns VM.)

    (replace (BRC.VM PROGRAM) of VM with PROGRAM)
    (replace (BRC.VM IP) of VM with 1)
    (replace (BRC.VM STATE) of VM with 'READY)
    VM])

(BRC.VM.OUT
  [LAMBDA (VM)                                               (* ; "Edited 26-May-2023 08:16 by PA")

         (* Executes the OUT instruction of the Brainfuck program in virtual machine VM by 
         printing to the OUT stream of VM the character with code equal to the value at 
         the memory cell pointed to by the data pointer.
         No newline is output after the character unless the code is |10.|
         Unprintable characters are printed as a black circle symbol.
         Returns VM.)

    (LET* ((MEMORY (fetch (BRC.VM MEMORY) of VM))
           (CHARCODE (ELT MEMORY (fetch (BRC.VM DATAPTR) of VM)))
           (STREAM (fetch (BRC.VM OUT) of VM)))
          (COND
             ((EQ CHARCODE 10)
              (TERPRI STREAM))
             ((OR (LESSP CHARCODE 32)
                  (GREATERP CHARCODE 126))
              (PRINTCCODE 127 STREAM))
             (T (PRINTCCODE CHARCODE STREAM)))
          VM])

(BRC.VM.PREVNEXT
  [LAMBDA (VM OPCODE)                                        (* ; "Edited 25-May-2023 03:06 by PA")

         (* Executes the PREV or NEXT instruction of the Brainfuck program in virtual 
         machine VM depending on the corresponding OPCODE.
         Generates MEMOUTOFBOUNDS error if the memory pointer ends up pointing outside of 
         the memory space. Returns VM.)

    (LET* ((CURPTR (fetch (BRC.VM DATAPTR) of VM))
           (NEWPTR (SELECTQ OPCODE
                       (PREV (SUB1 CURPTR))
                       (NEXT (ADD1 CURPTR))
                       CURPTR)))
          (if (AND (GEQ NEWPTR 0)
                   (LEQ NEWPTR BRC.MEMSIZE))
              then (replace (BRC.VM DATAPTR) of VM with NEWPTR)
            else (replace (BRC.VM STATE) of VM with (create BRC.ERROR
                                                           REASON _ 'MEMOUTOFBOUNDS
                                                           ADDRESS _ NEWPTR)))
          VM])

(BRC.VM.RESET
  [LAMBDA (VM CLEARPROG)                                     (* ; "Edited 20-May-2023 08:59 by PA")
                                                             (* ; "Edited  7-May-2023 10:37 by PA")
                                                             (* ; "Edited  6-May-2023 10:21 by PA")

         (* Clears the memory of VM to null bytes, and deletes any stored program if 
         CLEARPROG is T instead of the default NIL.
         VM is initialized to restart the program, or load a new one if CLEARPROG is T.
         Returns VM.)

    (bind (MEMORY _ (fetch (BRC.VM MEMORY) of VM)) for MEMPTR from 0 to (SUB1 BRC.MEMSIZE)
       do (SETA MEMORY MEMPTR 0))
    (replace (BRC.VM IP) of VM with 1)
    (replace (BRC.VM DATAPTR) of VM with 0)
    (replace (BRC.VM STATE) of VM with 'READY)
    (if CLEARPROG
        then (replace (BRC.VM PROGRAM) of VM with NIL))
    VM])

(BRC.VM.RUN
  [LAMBDA (VM STEPS)                                         (* ; "Edited 27-May-2023 08:47 by PA")
                                                             (* ; "Edited 26-May-2023 07:09 by PA")
                                                             (* ; "Edited 21-May-2023 08:38 by PA")
                                                             (* ; "Edited 14-May-2023 10:04 by PA")
                                                             (* ; "Edited  6-May-2023 10:16 by PA")

         (* VM runs the program until the end if STEPS is NIL, otherwise executes the next 
         STEPS instructions. A runtime error terminates the program.
         Returns VM.)

    (LET* [(PROGRAM (fetch (BRC.VM PROGRAM) of VM))
           (ENDIDX (fetch (BRC.PROGRAM LASTIP) of PROGRAM))
           (MAXSTEPS (if STEPS
                         then (MIN STEPS ENDIDX]

         (* The VM runs only if there is a program, the machine is ready, and the IP 
         hasn't reached the end of the program yet.)

          [AND PROGRAM (EQ (fetch (BRC.VM STATE) of VM)
                           'READY)
               (LEQ (fetch (BRC.VM IP) of VM)
                    ENDIDX)

         (* The machine must be READY for the fetch-decode-execute cycle to run.
         The cycle continues as long as either execution until program end is requested 
         and there are still instructions, or the requested steps haven't been completed 
         yet.)

               (bind (I _ 1)
                     (MNEMONIC _ NIL)
                     (PC _ 1)
                     (RUNSTATE _ (fetch (BRC.VM STATE) of VM))
                  while [AND (EQ RUNSTATE 'READY)
                             (OR (AND (NULL STEPS)
                                      (LEQ (fetch (BRC.VM IP) of VM)
                                           ENDIDX))
                                 (AND STEPS (LEQ I MAXSTEPS]
                  do (SETQ INSTRUCTION (ELT (fetch (BRC.PROGRAM INSTRUCTIONS) of PROGRAM)
                                            (fetch (BRC.VM IP) of VM)))
                     (SETQ MNEMONIC (if (LITATOM INSTRUCTION)
                                        then INSTRUCTION
                                      else (fetch (BRANCH MNEMONIC) of INSTRUCTION)))
                     (SETQ PC (fetch (BRC.VM IP) of VM))
                     (SELECTQ MNEMONIC
                         (NEXT (BRC.VM.PREVNEXT VM 'NEXT))
                         (PREV (BRC.VM.PREVNEXT VM 'PREV))
                         (INC (BRC.VM.INCDEC VM 'INC))
                         (DEC (BRC.VM.INCDEC VM 'DEC))
                         (IN (BRC.VM.IN VM))
                         (OUT (BRC.VM.OUT VM))
                         (JZ (BRC.VM.BRANCH VM INSTRUCTION))
                         (JNZ (BRC.VM.BRANCH VM INSTRUCTION))
                         NIL)
                     (if (AND STEPS (LEQ I MAXSTEPS))
                         then (SETQ I (ADD1 I)))
                     (replace (BRC.VM IP) of VM with (ADD1 PC))
                     (SETQ RUNSTATE (fetch (BRC.VM STATE) of VM]
          VM])
)

(RPAQQ BRC.MAXIP 30000)

(RPAQ? BRC.MEMSIZE 30000)
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (4068 25602 (BRC.BRANCHP 4078 . 4772) (BRC.MAKE.JNZ 4774 . 6470) (BRC.MAKE.JZ 6472 . 
7583) (BRC.MAKE.VM 7585 . 8152) (BRC.PARSE 8154 . 11570) (BRC.PARSE.INSTRUCTION 11572 . 12845) (
BRC.VALIDATE.BRANCHES 12847 . 14366) (BRC.VM.BRANCH 14368 . 15508) (BRC.VM.IN 15510 . 17649) (
BRC.VM.INCDEC 17651 . 18589) (BRC.VM.LOAD 18591 . 19143) (BRC.VM.OUT 19145 . 20147) (BRC.VM.PREVNEXT 
20149 . 21218) (BRC.VM.RESET 21220 . 22249) (BRC.VM.RUN 22251 . 25600)))))
STOP
